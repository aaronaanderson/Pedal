# glfw for window creation
set(GLFW_BUILD_DOCS OFF CACHE BOOL "GLFW Documentation" FORCE)
set(GLFW_INSTALL OFF CACHE BOOL "Installation Target" FORCE)
add_subdirectory(glfw)

# imgui for gui
add_library(imgui STATIC imgui/imgui_demo.cpp imgui/imgui_draw.cpp
                         imgui/imgui_widgets.cpp imgui/imgui.cpp)
set_target_properties(imgui PROPERTIES
    DEBUG_POSTFIX d
    CXX_STANDARD 14
    CXX_STANDARD_REQUIRED ON
)
target_include_directories(imgui PUBLIC imgui)

# gl3w that came with imgui to load GL functions
add_library(gl3w STATIC imgui/examples/libs/gl3w/GL/gl3w.c)
set_target_properties(gl3w PROPERTIES
    DEBUG_POSTFIX d
    CXX_STANDARD 14
    CXX_STANDARD_REQUIRED ON
)
target_include_directories(gl3w PUBLIC imgui/examples/libs/gl3w)

# common example app functionality
add_library(example_app STATIC example_app.cpp)
set_target_properties(example_app PROPERTIES
    DEBUG_POSTFIX d
    CXX_STANDARD 14
    CXX_STANDARD_REQUIRED ON
)
target_include_directories(example_app PUBLIC .)
target_link_libraries(example_app PUBLIC glfw gl3w imgui)

# list of examples, give `examplename` if example file is `examplename.cpp`
set(example_list hello_pedal)

# go through example targets
foreach(example ${example_list})
    add_executable(${example} "${example}.cpp")

    set_target_properties(${example} PROPERTIES
        DEBUG_POSTFIX d
        CXX_STANDARD 14
        CXX_STANDARD_REQUIRED ON
    )

    target_include_directories(${example} PRIVATE .)
    target_link_libraries(${example} PRIVATE pedal example_app)
endforeach()
